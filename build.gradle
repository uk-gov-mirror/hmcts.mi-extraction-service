plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.6.1'
  id 'org.owasp.dependencycheck' version '6.5.0.1'
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'org.sonarqube' version '3.3'
  id 'info.solidsoft.pitest' version '1.7.0'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

allprojects {
  sourceCompatibility = '11'
  targetCompatibility = '11'
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked"
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  useJUnitPlatform()

  outputs.upToDateWhen { false }
}

task integrationPreTest(type: Test, description: 'Runs the pre-job integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  useJUnitPlatform()
}

task integrationPostTest(type: Test, description: 'Runs the post-job integration verfications.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  useJUnitPlatform()
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.24'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDirectory = new File(rootDir, 'config/checkstyle')
}

pmd {
  toolVersion = "6.21.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/jacocoTestReport.xml")
    html.destination file("${buildDir}/reports/jacoco/jacocoTestReport.html")
  }
}

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

def sonarExclusions = [
    '**uk/gov/hmcts/reform/mi/miextractionservice/MiExtractionServiceApplication.java',
    '**uk/gov/hmcts/reform/mi/miextractionservice/config/**',
    '**uk/gov/hmcts/reform/mi/miextractionservice/domain/**',
    '**uk/gov/hmcts/reform/mi/miextractionservice/util/FileUtils**'
]

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: mi-extraction-service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:mi-extraction-service"
    property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/jacocoTestReport.xml"
    property "sonar.exclusions", sonarExclusions.join(", ")
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all {
      def isNonStable = { String version ->
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
          version ==~ /(?i).*[.-]\$qualifier[.\\d-+]*/
        }
      }
      if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
        selection.reject('Release candidate')
      }
    }
  }
}

def versions = [
  azureSpringBoot  : '3.11.0',
  azureStorage     : '12.14.2',
  commonsIo        : '2.11.0',
  dumbster         : '1.7.1',
  failsafe         : '2.4.4',
  hmctsLogging     : '5.1.9',
  hmctsNotify      : '3.17.2-RELEASE',
  junit            : '5.8.2',
  miCoreLib        : '3.6.0',
  openCsv          : '5.5.2',
  piTest           : '1.7.3',
  piTestGradle     : '1.7.0',
  piTestSonar      : '0.5',
  springBootMail   : '2.6.1',
  testContainers   : '1.16.2',
  zip4j            : '2.9.1',
  jcraft           : '0.1.55',
  bouncycastle     : '1.70'
]

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.70'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '31.0.1-jre') {
      entry 'guava'
    }
    dependency group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.9.0'
    dependency group: 'com.squareup.retrofit2', name: 'adapter-rxjava', version: '2.9.0'
    // CVE-2020-9488
    dependencySet(group: 'org.apache.logging.log4j', version: '2.17.1') {
      entry 'log4j-to-slf4j'
      entry 'log4j-api'
    }
    dependencySet(group: 'ch.qos.logback', version: '1.2.10') {
      entry 'logback-classic'
      entry 'logback-core'
    }
    dependencySet(group: 'io.netty', version: '4.1.72.Final') {
      entry 'netty-buffer'
      entry 'netty-codec'
      entry 'netty-codec-dns'
      entry 'netty-codec-http'
      entry 'netty-codec-http2'
      entry 'netty-codec-socks'
      entry 'netty-common'
      entry 'netty-handler'
      entry 'netty-handler-proxy'
      entry 'netty-resolver'
      entry 'netty-resolver-dns'
      entry 'netty-resolver-dns-classes-macos'
      entry 'netty-resolver-dns-native-macos'
      entry 'netty-transport'
      entry 'netty-transport-classes-epoll'
      entry 'netty-transport-native-epoll'
      entry 'netty-transport-classes-kqueue'
      entry 'netty-transport-native-kqueue'
      entry 'netty-transport-native-unix-common'
    }
    dependency group: 'commons-io', name: 'commons-io', version: versions.commonsIo
  }
}

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven {
    url "https://jitpack.io"
  }
}

dependencies {

  implementation 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'

  implementation group: 'com.azure.spring', name: 'azure-spring-boot-starter', version: versions.azureSpringBoot
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: versions.springBootMail
  implementation group: 'com.azure', name: 'azure-storage-blob', version: versions.azureStorage

  implementation group: 'com.opencsv', name: 'opencsv', version: versions.openCsv
  implementation group: 'net.lingala.zip4j', name: 'zip4j', version: versions.zip4j
  implementation group: 'net.jodah', name: 'failsafe', version: versions.failsafe
  implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo

  // Reform libraries
  implementation group: 'com.jcraft', name: 'jsch', version: versions.jcraft

  implementation group: 'com.github.hmcts', name: 'mi-core-lib', version: versions.miCoreLib
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.hmctsLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.hmctsLogging
  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: versions.hmctsNotify

  implementation group: 'org.pitest', name: 'pitest', version: versions.piTest
  implementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.piTestGradle
  implementation group: 'org.codehaus.sonar-plugins', name: 'sonar-pitest-plugin', version: versions.piTestSonar

  implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: versions.bouncycastle
  implementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: versions.bouncycastle
  implementation group: 'org.bouncycastle', name: 'bcpg-jdk15on', version: versions.bouncycastle


  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit

  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  functionalTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: versions.springBootMail
  functionalTestImplementation group: 'com.azure', name: 'azure-storage-blob', version: versions.azureStorage

  functionalTestImplementation group: 'com.github.hmcts', name: 'mi-core-lib', version: versions.miCoreLib

  functionalTestImplementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: versions.hmctsNotify

  functionalTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit
  functionalTestImplementation group: 'com.jcraft', name: 'jsch', version: versions.jcraft

  functionalTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: versions.testContainers
  functionalTestImplementation group: 'com.opencsv', name: 'opencsv', version: versions.openCsv
  functionalTestImplementation group: 'net.lingala.zip4j', name: 'zip4j', version: versions.zip4j
  functionalTestImplementation group: 'com.github.kirviq', name: 'dumbster', version: versions.dumbster
  functionalTestImplementation group: 'net.jodah', name: 'failsafe', version: versions.failsafe

  functionalTestImplementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: versions.bouncycastle
  functionalTestImplementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: versions.bouncycastle
  functionalTestImplementation group: 'org.bouncycastle', name: 'bcpg-jdk15on', version: versions.bouncycastle

  functionalTestImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  integrationTestImplementation 'org.projectlombok:lombok'
  integrationTestAnnotationProcessor 'org.projectlombok:lombok'
  integrationTestImplementation group: 'com.jcraft', name: 'jsch', version: versions.jcraft

  integrationTestImplementation group: 'com.azure', name: 'azure-storage-blob', version: versions.azureStorage

  integrationTestImplementation group: 'com.github.hmcts', name: 'mi-core-lib', version: versions.miCoreLib

  integrationTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions.junit

  integrationTestImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}

test {
  useJUnitPlatform()
}

integrationPreTest {
  filter {
    includeTestsMatching "*PreDeployTest"
  }
}

integrationPostTest {
  filter {
    includeTestsMatching "*PostDeployTest"
  }
}

// Ignoring components consisting mainly of try-with-resource until Java 11+ pitest filter is fixed.
pitest {
  pitestVersion = '1.5.0'
  targetClasses = ['uk.gov.hmcts.reform.mi.miextractionservice.*']
  excludedClasses = ['uk.gov.hmcts.reform.mi.miextractionservice.MiExtractionServiceApplication*',
                     'uk.gov.hmcts.reform.mi.miextractionservice.component.compression.GzipCompressionComponentImpl*',
                     'uk.gov.hmcts.reform.mi.miextractionservice.config.*',
                     'uk.gov.hmcts.reform.mi.miextractionservice.domain.*',
                     'uk.gov.hmcts.reform.mi.miextractionservice.exception.*',
                     'uk.gov.hmcts.reform.mi.miextractionservice.util.FileUtils*']
  testSourceSets = [sourceSets.test]
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 70 // Lower boundary to pass. Ensure unkilled mutations are solely part of try-with-resource blocks or file related.
  verbose = true
  junit5PluginVersion = '0.12'
}

mainClassName = 'uk.gov.hmcts.reform.mi.miextractionservice.MiExtractionServiceApplication'

bootJar {
  archiveFileName = project.name + '.jar'

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}
